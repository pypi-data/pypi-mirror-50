#! /usr/bin/env neu

* air_say
** air_joined_str
*** air_mul
**** air_div
***** 48
***** 94
**** 100
*** '% of AST nodes implemented'

* print("This is a regular Python expression...")

* air_say '... and this is inline ASTIR (Abstract Syntax Tree Intermediate Representation)...'

* air_setv
** this
** "... which can also be expressed hierarchially"
* air_say this

* air_say
** ["a", 'Python', "list"]
** is also a regular Python expression,
** "but under ASTIR"

* air_say
** 'Now for a lil math: 7+13+9='
** air_add
*** 7
*** 13
*** air_add 6 3
* print(f'15+21={15 + 21} also works of course')

* air_say
** 'Testing random combined math:'
** air_mul
*** 5
*** 31
*** air_div 10 3
*** 7.9
*** air_sub 99.3 100
** with some text after it

* air_say
** Testing unary 'not'; should yield True:
** air_not 0
** and False:
** air_not
*** 13

* air_say
** Testing booleans...

* air_say
** 'And' table results:
** air_and
*** 0
*** 0
** air_and
*** 0
*** 1
** air_and
*** 1
*** 0
** air_and 1 1

* air_say
** 'Or' table results:
** air_or
*** 0
*** 0
** air_or 0 1
** air_or
*** 1
*** 0
** air_or 1 1

* air_say
** Test comparison 5 == 4+1 == 3+2:
** air_eq
*** 5
*** air_add 4 1
*** air_add
**** 3
**** 2
*** 
** 

* air_call print 'Now we can call functions!'

* air_nop
** Everything in this node is ignored
** air_call
*** air_say
*** This call syntax looks a bit weird...
*** But it'll make sense in time, I hope
** air_call
*** air_say
*** This string has embedded "double quotes" and 'single quotes' in it, and even "a 'nested example'".
** air_while
*** 
** air_return None

* air_import
** os
* air_import random
* air_say 'Imports:' os random

* air_if
** air_eq
*** 5
*** 6
** air_say "They're equal!"
** air_say "They aren't equal..."

* air_for
** idx
** air_call range 1 5
** air_say idx "potato"

* air_def
** hello
** args
*** name
** body
*** air_say 'Hi there' name '!'
** decorators
** returns
* hello('Drew')

* air_class
** my_life
** body
*** air_setv fav_num 13
*** air_setv
**** now_playing
**** "Chat Wars"
** 
* print(f"Favorite number is {my_life.fav_num}")
* air_say
** Current obsession:
** air_call
*** getattr
*** my_life
*** 'now_playing'
* air_del my_life

* air_assert True 'Some random message that never shows'

* air_try
** body
*** air_say 'Testing exceptions'
*** air_raise
**** air_call
***** Exception
***** Some random reason
** handlers
*** air_except_handler
**** Exception
**** e
**** body
***** air_say 
****** "Raising for:"
****** air_call str e

* air_say
** air_dict
*** 145
**** 'one quatro go'
*** 'a string'
**** 'says blah'
*** True
**** 13

* air_def
** arg_ts
** args
*** a
*** air_kwd
**** e
**** 27
*** air_starred
**** b
*** air_kwd
**** c
**** None
*** air_kwd
**** d
** body
*** air_say a b c d e
* air_call
** arg_ts
** 29
** air_starred
*** air_list
**** 'a disassociated list' 
**** 'of strings'
** air_kwd
*** c
*** True
** air_kwd
*** air_dict
**** 'x'
***** 'ekans'
**** 'y'
***** 'Yugioh'
**** 'z'
***** 'Zabuza'

* air_say
** 'Testing'
** air_list 'list' 'of' 'elements'

* air_say
** 'Result of slicing a list: '
** air_slice
*** air_list 1 2 3
*** -1

* air_with
** items
*** fo
**** open('file.txt', 'w')
** body
*** fo.write('Some random text')

* air_say 'Goodbye ASTIR'

* print("=============================")

* say Hello natural language programming
** add_adapt_handler
*** intent_parts
**** 'say (?P<req_text>.+)'
*** body
**** air_say text
* say The above sub definition is visible beyond!

* air_setv
** a_string 
** 'This is a string'
* print(a_string)
* add_adapt_handler
** intent_parts
*** 'replace (?P<req_txt>.+) with (?P<req_rep>.+) in (?P<req_var>\w+)'
** body
*** air_setv
**** globals()[var]
**** globals()[var].replace(txt, rep)
* replace string with modified string in a_string
* print(a_string)

* find average of 3 and 5
** add_adapt_handler
*** intent_parts
**** 'find average of (?P<req_num1>\d+) and (?P<req_num2>\d+)'
*** body
**** x_setv('num1', num1)
** get and sum the numbers
*** add_adapt_handler
**** intent_parts
***** 'get and sum the numbers'
**** body
***** x_setv('num_sum', int(num1) + int(num2))
** divide by 2
*** add_adapt_handler
**** intent_parts
***** 'divide by <<req_amount>>'
**** body
***** x_setv('num_ave', x_getv('num_sum') / int(amount))
** output result
*** add_adapt_handler
**** intent_parts
***** 'output result'
**** body
***** air_setv
****** num_ave
****** x_getv("num_ave")
***** print(f'Average of {num1} and {num2} is {num_ave}')
* find average of 25 and 200
* 
